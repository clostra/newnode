buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.0'
    }
}

plugins {
    id 'com.vanniktech.maven.publish' version '0.18.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 32

    new File(System.getenv('NDK') + '/source.properties').eachLine { line ->
        def matcher = line =~ /Pkg.Revision = (.*)/
        if (matcher) {
            ndkVersion = matcher[0][1]
            return true
        }
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 32
        new File('../constants.h').eachLine { line ->
            def matcher = line =~ /VERSION "(.*)"/
            if (matcher) {
                versionName matcher[0][1]
                return true
            }
        }
        versionCode versionName.replace(".", "").toInteger()
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        buildConfigField "int", "VERSION_CODE", "$versionCode"
        consumerProguardFiles 'lib-proguard-rules.txt'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'PrivateApi','GradleDynamicVersion','UnsafeDynamicallyLoadedCode'
    }
}

allprojects {
    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            version = android.defaultConfig.versionName
        }
    }
}

dependencies {
    implementation 'com.bugsnag:bugsnag-android:4.7.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.gms:play-services-nearby:18.2.0'
}
