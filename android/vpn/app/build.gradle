apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    new File(System.getenv('NDK') + '/source.properties').eachLine { line ->
        def matcher = line =~ /Pkg.Revision = (.*)/
        if (matcher) {
            ndkVersion = matcher[0][1]
            return true
        }
    }

    signingConfigs {
       release {
           storeFile file(RELEASE_STORE_FILE)
           storePassword RELEASE_STORE_PASSWORD
           keyAlias RELEASE_KEY_ALIAS
           keyPassword RELEASE_KEY_PASSWORD
           v1SigningEnabled true
           v2SigningEnabled true
       }
    }

    defaultConfig {
        applicationId "com.clostra.newnode.vpn"
        minSdkVersion 29
        targetSdkVersion 29

        new File('../../constants.h').eachLine { line ->
            def matcher = line =~ /VERSION "(.*)"/
            if (matcher) {
                versionName matcher[0][1]
                return true
            }
        }
        versionCode versionName.replace(".", "").toInteger()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    // include AAR directly (must include dependencies)
    implementation project(':newnode')
    implementation 'com.bugsnag:bugsnag-android:4.5.0'
    implementation 'com.google.android.gms:play-services-nearby:17.+'

    // use jcenter
    //implementation 'com.clostra.newnode:newnode:+'
}
