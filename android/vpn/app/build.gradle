apply plugin: 'com.android.application'

android {
    compileSdkVersion 33

    new File(System.getenv('NDK') + '/source.properties').eachLine { line ->
        def matcher = line =~ /Pkg.Revision = (.*)/
        if (matcher) {
            ndkVersion = matcher[0][1]
            return true
        }
    }

    signingConfigs {
       release {
           storeFile file(RELEASE_STORE_FILE)
           storePassword RELEASE_STORE_PASSWORD
           keyAlias RELEASE_KEY_ALIAS
           keyPassword RELEASE_KEY_PASSWORD
           v1SigningEnabled true
           v2SigningEnabled true
       }
    }

    defaultConfig {
        applicationId "com.clostra.newnode.vpn"
        minSdkVersion 16
        targetSdkVersion 33

        new File('../../constants.h').eachLine { line ->
            def matcher = line =~ /VERSION "(.*)"/
            if (matcher) {
                versionName matcher[0][1]
                return true
            }
        }
        versionCode versionName.replace(".", "").toInteger()
    }

    /*
    externalNativeBuild {
        ndkBuild {
            path "jni/badvpn/Android.mk"
        }
    }
    */

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.multidex:multidex:2.0.1'

    // include AAR directly (must include dependencies)
    releaseImplementation files('../../build/outputs/aar/newnode-release.aar')
    debugImplementation files('../../build/outputs/aar/newnode-debug.aar')
    implementation 'com.bugsnag:bugsnag-android:4.7.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.gms:play-services-nearby:18.3.0'

    // use mavenCentral
    //implementation 'com.clostra.newnode:newnode:+'
}
